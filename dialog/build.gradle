plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.koonny.dialog'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.8.0'
}

group = 'com.koonny.dialog'
version = '1.0.1-SNAPSHOT'

publishing {
    publications {
        maven(MavenPublication) {
            afterEvaluate {
                artifact(tasks.getByName("bundleReleaseAar"))
            }
            artifactId = 'dialog'
            pom {
                name = 'dialog'
                description = 'A android module'
                url = 'https://github.com/jyygithub/koonny-dialog'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jiangyy'
                        name = 'JIANGYY'
                        email = 'jiangyy@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/jyygithub/koonny-dialog.git'
                    developerConnection = 'scm:git:ssh://github.com/jyygithub/koonny-dialog.git'
                    url = 'https://github.com/jyygithub/koonny-dialog'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}